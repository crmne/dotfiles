#!/usr/bin/env fish

set -l code_dir "$HOME/Code"

for bin in gh git
  if not type -q $bin
    echo "Missing dependency: $bin"
    exit 1
  end
end

mkdir -p "$code_dir"

gh repo list --limit 1000 --json name,sshUrl,isArchived --jq '.[] | select(.isArchived | not) | "\(.name) \(.sshUrl)"' | while read -l name url
  set -l repo_dir "$code_dir/$name"

  if test -e "$repo_dir"
    echo "Skip: $repo_dir already exists"
    continue
  end

  read -l -P "Clone $name? [y/N] " confirm < /dev/tty
  if string match -qr '^(y|yes)$' -- (string lower -- $confirm)
    git clone $url "$repo_dir"
  else
    echo "Skip: $name"
  end
end
